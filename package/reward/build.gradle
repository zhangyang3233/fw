// 声明是Android程序
apply plugin: 'com.android.application'

//def releaseTime() {
//    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
//}

android {
    // 编译SDK的版本
    compileSdkVersion 23
    // build tools的版本
    buildToolsVersion "23.0.3"
    defaultConfig {
        // 在BuildConfig生成一个名字叫 IS_SHOW_LOG boolean类型的变量,值为true
        buildConfigField "boolean", "IS_SHOW_LOG", "true"
        // 应用的包名
        applicationId "com.hongyu.reward"
        // 支持的最小的 Android 版本
        minSdkVersion 16
        targetSdkVersion 22
        // 程序内部版本号
        versionCode 7
        // 对外版本名称
        versionName "1.0.7"
        // dex突破65535的限制
        multiDexEnabled false
        // 默认是self的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "self"]
    }


    // 签名配置
    signingConfigs {
        // 配置的名字(可以自己定义)
        debugAndRelease {
            keyAlias 'weiyixuanshang'
            keyPassword 'weiyixuanshang11'
            storeFile file('wyxs.jks')
            storePassword 'weiyixuanshang11'
        }
    }
    buildTypes { // 构建类型,一般有release 和 debug. 有些公司会用 release 和 dev,不过都是一个意思
        release {
            // 签名配置, 这里引用 debugAndRelease
            signingConfig signingConfigs.debugAndRelease
            // 支持混淆
            minifyEnabled true
            // 混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 是否可以debug
            debuggable true
            // 是否可以debug jni
            jniDebuggable false
            // 在BuildConfig生成一个名字叫 IS_SHOW_LOG boolean类型的变量,值为false(这里复写了defaultConfig里面的值)
            buildConfigField "boolean", "IS_SHOW_LOG", "false"
            buildConfigField "boolean", "IS_DEBUG", "false"
            // 版本名称后缀
            versionNameSuffix "-release"
            // 使用zipAlign优化压缩包
            zipAlignEnabled true
            // 去除没有用到的资源文件
            shrinkResources true
            // 重命名 apk
//            applicationVariants.all { variant ->
//                variant.outputs.each { output ->
//                    def outputFile = output.outputFile
//                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
//                        // 输出apk名称为demolist_v1.0_2015-01-15_wandoujia.apk
//                        def fileName = "demolist_${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
//                        output.outputFile = new File(outputFile.parent, fileName)
//                    }
//                }
//            }
        }

        debug {
            // debug也用release版本的签名,因为有时候调用微信支付宝什么要用
            signingConfig signingConfigs.debugAndRelease
            // debug 版本就不用支持混淆了
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            jniDebuggable true
            zipAlignEnabled false
            shrinkResources false
            // 在BuildConfig生成一个名字叫 IS_SHOW_LOG boolean类型的变量,值为false(这里复写了defaultConfig里面的值)
            buildConfigField "boolean", "IS_SHOW_LOG", "true"
            buildConfigField "boolean", "IS_DEBUG", "true"
        }
    }

    // 多渠道打包
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_CODE: name]
    }

    // 移除lint检查的error
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    // 构建变体
    productFlavors {
        yingyongbao {}
        baidu {}
        xiaomi {}
        huawei {}
        wandoujia {}
        anzhi {}
//        personal {}
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.contains('AndroidTest')) {
        task.enabled = false
    }
}

dependencies {
    // 编译libs目录下所有的jar包
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // 编译依赖
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23+'
    // 编译 external 目录下的 imageblur 模块
    compile project(':external:image:imageblur')
    compile project(':external:utils')
    compile project(':external:nfc')
    compile project(':external:errorpage')
    compile project(':external:customtoast')
    compile project(':external:flowlayout')
    compile project(':framework:http')
    compile project(':framework:widgets')
    compile project(':external:refreshable')
    compile project(':external:uicomp')
    compile project(':external:dialoghelper')
    compile project(':external:magicindicator')
    compile project(':external:PushSDK')
    compile 'com.jph.takephoto:takephoto_library:4.0.3'
    compile 'com.android.support:cardview-v7:23.+'
    compile 'org.litepal.android:core:1.3.2'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'me.relex:circleindicator:1.2.1@aar'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.pgyersdk:sdk:2.4.0'
    compile 'com.bigkoo:convenientbanner:2.0.5'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
//    debugCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}
